/*
 * Copyright (C) 2016 Netflix, Inc.
 *
 *     This file is part of IMF Conversion Utility.
 *
 *     IMF Conversion Utility is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     IMF Conversion Utility is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with IMF Conversion Utility.  If not, see <http://www.gnu.org/licenses/>.
 */

// 1. project description
description = "IMF conversion main entry point"

// 2. dependencies
dependencies {
    compile project(':dpp-conversion')
    compile project(':itunes-conversion')
}

// 3. create a distribution
apply plugin: 'application'

mainClassName = "com.netflix.imfutility.ImfUtility"

// 4. copy default tools (validation, ttml-to-stl) to the tools folder
// these paths are also specified in the Constants! Make sure that they correspond.
task copyDefaultTools(type: Copy) {
    from '../imf-validation/build/libs'
    from '../ttml-to-stl/build/libs'
    into "${buildDir}/tools"
}

evaluationDependsOn(':imf-validation')
evaluationDependsOn(':ttml-to-stl')
copyDefaultTools.dependsOn(':imf-validation:assemble', ':ttml-to-stl:assemble')
classes.dependsOn(copyDefaultTools)

// 5. customize distribution:
// - copy default tools into tools folder
// - copy sample config.xml and licence
distributions {
    main {
        contents {
            from(copyDefaultTools) {
                into "tools"
            }
            from('../sample') {
            }
            from('../COPYING') {
            }
            from('../README.md') {
            }
        }
    }
}